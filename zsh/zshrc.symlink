# vim: fdm=marker
# zmodload zsh/zprof
PROFILE_STARTUP=false
if [[ "$PROFILE_STARTUP" == true ]]; then
    # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
    PS4=$'%D{%M%S%.} %N:%i> '
    exec 3>&2 2>$HOME/tmp/startlog.$$
    setopt xtrace prompt_subst
fi
fpath=($HOME/.zfunc $fpath)
# zenv ---------------------------------{{{
DISABLE_AUTO_UPDATE='true'
skip_global_compinit=1
typeset -A ZI
ZI[BIN_DIR]="${HOME}/.zi/bin"
source "${ZI[BIN_DIR]}/zi.zsh"

autoload -Uz _zi
(( ${+_comps} )) && _comps[zi]=_zi

zinit snippet OMZL::compfix.zsh
zinit snippet OMZL::completion.zsh
zinit snippet OMZL::directories.zsh
zinit snippet OMZL::functions.zsh
zinit snippet OMZL::grep.zsh
zinit snippet OMZL::key-bindings.zsh
zinit snippet OMZL::misc.zsh
zinit snippet OMZL::nvm.zsh
zinit snippet OMZL::spectrum.zsh
zinit snippet OMZL::termsupport.zsh
zinit snippet OMZL::theme-and-appearance.zsh

zinit ice wait lucid multisrc'asdf.sh ~/.config/asdf-direnv/zshrc ~/dots/zsh/aliases.zsh.symlink' compclear
zinit load asdf-vm/asdf

zinit lucid wait for \
    agkozak/zhooks

fpath=( ~/dots/zsh/func "${fpath[@]}" )

for f in ~/dots/zsh/func/*(*); do
    autoload -Uz $f
done

#}}}
# Common Directories -------------------{{{
dots=~/dots
hash -d dots=$dots
hash -d itermscripts=~/Library/Application\ Support/iTerm2/Scripts
hash -d zettel=~/vaults/zettel

if whence keybase &>/dev/null; then
    if [[ $(uname) == 'Darwin' ]]; then
        keybase=/Volumes/Keybase/private/edjojob
        keybasepub=/Volumes/Keybase/public/edjojob
    elif [[ $(uname) == 'Linux' ]]; then
        keybase=/keybase/private/edjojob
        keybasepub=/keybase/public/edjojob
    fi
    : ~keybase
    : ~keybasepub
fi
#}}}
# Plugins Settings ---------------------{{{
# zsh-syntax-highlighting {{{
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern)
export ZSH_HIGHLIGHT_HIGHLIGHTERS
typeset -A ZSH_HIGHLIGHT_STYLES
# }}}
# todo.sh {{{
export TODO_ROOT="$HOME/.todo"
export TODO_DIR="$TODO_ROOT/todo"

# Your todo/done/report.txt locations
export TODO_FILE="$TODO_DIR/todo.txt"
export DONE_FILE="$TODO_DIR/done.txt"
export REPORT_FILE="$TODO_DIR/report.txt"

# You can customize your actions directory location
#export TODO_ACTIONS_DIR="$HOME/.todo.actions.d"
export TODO_ACTIONS_DIR="$TODO_ROOT/actions"

export TODO_NOTES_DIR="$TODO_ROOT/notes"
#}}}
#}}}
# zsh core settings --------------------{{{
# editor {{{
# bind UP and DOWN arrow keys
zmodload zsh/terminfo
bindkey "$terminfo[kcuu1]" fzf-history-widget
bindkey "$terminfo[kcud1]" fzf-history-widget
bindkey -M vicmd 'k' fzf-history-widget
bindkey -M vicmd 'j' fzf-history-widget
autoload edit-command-line; zle -N edit-command-line
bindkey -M vicmd v edit-command-line
bindkey -M vicmd q push-line
#}}}

# history {{{
HISTFILE="$HOME/.zsh_history"
SAVEHIST=999999999
HISTSIZE=999999999

alias history='fc -il1'

# man zshoptions
setopt append_history
setopt extended_history # have timestamps
setopt inc_append_history_time
setopt hist_expire_dups_first
setopt hist_ignore_all_dups
setopt hist_fcntl_lock
setopt hist_no_store
setopt hist_ignore_space
setopt hist_lex_words
setopt hist_verify
setopt interactive_comments

unsetopt share_history

# }}}

setopt extendedglob
#}}}
# PATH ---------------------------------{{{
export XDG_CONFIG_HOME=$HOME/.config
#}}}
# Default Path -------------------------{{{
path=(
    /usr/local/bin
    /usr/local/sbin
    /usr/bin
    /bin
    /usr/sbin
    /sbin
    ${HOME}/.fzf/bin
    ${HOME}/.local/bin
    ${HOME}/.bin
)
typeset -U path
export path
# }}}
# Colors -------------------------------{{{
zinit ice atclone'dircolors -b ~/dots/dir_colors.symlink > clrs.zsh' \
    atpull'%atclone' pick"clrs.zsh" nocompile'!' \
    atload'zstyle ":completion:*" list-colors “${(s.:.)LS_COLORS}”'
zinit light trapd00r/LS_COLORS


# Use gnu's ls so don't need these lscolors
if [[ $(uname) == 'Darwin' && -n $LSCOLORS ]]; then
  unset LSCOLORS
fi

#}}}
# Add tools {{{
# }}}
# Other files --------------------------{{{
[ -f ~/.local_zshrc ] && source ~/.local_zshrc
source $dots/zsh/zsh_theme
source $dots/zsh/fzf.zsh
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh && bindkey -r '^T' && bindkey '^P' fzf-file-widget
#}}}


# Tmux detection -----------------------{{{

fixssh() {
    for key in SSH_AUTH_SOCK SSH_CONNECTION SSH_CLIENT MACOS_MODE; do
        if (tmux show-environment | grep "^${key}" > /dev/null); then
            value=`tmux show-environment | grep "^${key}" | sed -e "s/^[A-Z_]*=//"`
            export ${key}="${value}"
        fi
    done
}

if [ -z $TMUX ]; then
    test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"
else
    fixssh
fi
#}}}
# SSH Detection ------------------------{{{
export GPG_TTY=$(tty)
if [[ -n "$SSH_CONNECTION" ]] ;then
    export PINENTRY_USER_DATA="USE_CURSES=1"
fi
# }}}

zinit wait lucid for \
    atinit"zicompinit; zicdreplay" \
        z-shell/F-Sy-H \
    as"completion" \
        zsh-users/zsh-completions \
        srijanshetty/zsh-pandoc-completion

zinit ice wait'!' lucid atload'source ~/dots/zsh/theme_vars'
zinit load zsh-git-prompt/zsh-git-prompt

autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
  compinit
done
compinit -C

if [[ "$PROFILE_STARTUP" == true ]]; then
    unsetopt xtrace
    exec 2>&3 3>&-
fi
# zprof
